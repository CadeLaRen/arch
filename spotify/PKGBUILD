# Maintainer: Gadget3000 <gadget3000@msn.com>
# Contributor: Eothred <yngve.levinsen@gmail.com>

pkgname=spotify
pkgver=0.9.17.8
_anotherpkgver=.gd06432d.31-1
pkgrel=1
pkgdesc="A proprietary peer-to-peer music streaming service"
arch=('x86_64')
license=('custom:"Copyright (c) 2006-2010 Spotify Ltd"')
install=spotify.install
url="http://www.spotify.com"
changelog='spotify.changelog'
options=('!strip')
conflicts=('spotify-beta')

source=("http://repository.spotify.com/pool/non-free/s/${pkgname}/${pkgname}-client-0.9.17_${pkgver}${_anotherpkgver}_amd64.deb"
'spotify'
'spotify.protocol'
)
md5sums=('69a320368ded33cfcc4e3bb4cc71575c'
'd443873ef440a6b59d2e3deef64ab74b'
'ef25ddc5b6bf8fe1a0d64cbd79e1f7b4')

depends=("alsa-lib>=1.0.14" "glibc>=2.6" "libxss>=1.2.0" "qt4>=4.5.0" "openssl" "nspr>=4.0" "nspr<5.0" "gconf" "nss" "dbus" "systemd" "libgcrypt15" "gtk2")
optdepends=('desktop-file-utils: Adds URI support to compatible desktop environments'
            'ffmpeg-compat: Adds support for playback of local files'
						'libnotify: Adds desktop notifications'
						)

package() {
  cd "${srcdir}"

	tar -xJf data.tar.xz -C "${pkgdir}"

  install -d "${pkgdir}/usr/share/"
  mv "${pkgdir}/opt/spotify" "${pkgdir}/usr/share/"
  rm -r "${pkgdir}/opt"

  # Bin Script
  install -Dm755 "${srcdir}/spotify" "${pkgdir}/usr/bin/spotify"

  # Copy license
  install -Dm644 "${pkgdir}/usr/share/doc/${pkgname}-client-0.9.17/copyright" "${pkgdir}/usr/share/licenses/${pkgname}/copyright"

  # Copy protocol file if KDE is installed
  if [ -f /usr/bin/startkde ]; then
    echo "Installing with KDE support"
    install -Dm644 "${srcdir}/spotify.protocol" "${pkgdir}/usr/share/kde4/services/spotify.protocol"
  fi
}
