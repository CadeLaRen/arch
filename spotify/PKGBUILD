# Maintainer: Gadget3000 <gadget3000@msn.com>
# Contributor: Eothred <yngve.levinsen@gmail.com>

pkgname=spotify
pkgver=0.9.1.55
_anotherpkgver=.gbdd3b79.203-1
pkgrel=2
pkgdesc="A proprietary peer-to-peer music streaming service"
arch=('x86_64' 'i686')
license=('custom:"Copyright (c) 2006-2013 Spotify Ltd"')
install=spotify.install
url="http://www.spotify.com"
changelog='spotify.changelog'
options=('!strip')
conflicts=('spotify-beta')

if [ "${CARCH}" = "x86_64" ]; then
  md5sums=('5be8c87214685bab303f4c4ec02d8264')
  _carch=_amd64
elif [ "${CARCH}" = "i686" ]; then
  md5sums=('4aeb0de3138d9b89a805bde84a2ac6c8')
  _carch=_i386
fi

md5sums+=('53702d38b8bb0e1a7738805449214b54'
'ef25ddc5b6bf8fe1a0d64cbd79e1f7b4')

depends=("alsa-lib>=1.0.14" "glibc>=2.6" "libxss" "qtwebkit" "gconf" "nspr>=4.0" "nspr<5.0" "nss")
optdepends=('desktop-file-utils: Adds URI support to compatible desktop environments'
'ffmpeg-spotify: Adds playback support for local files')
source=("http://repository.spotify.com/pool/non-free/s/${pkgname}/${pkgname}-client_${pkgver}${_anotherpkgver}${_carch}.deb"
'spotify'
'spotify.protocol')

package() {
  cd "${srcdir}"

  ar x "${pkgname}-client_${pkgver}${_anotherpkgver}${_carch}.deb" > /dev/null
  tar -xzf data.tar.gz -C "${pkgdir}"

  install -d "${pkgdir}/opt/spotify/libs/"

  # Bin Script
  install -Dm755 "${srcdir}/spotify" "${pkgdir}/usr/bin/spotify"

  # libplc4.so
  ln -s /usr/lib/libplc4.so "${pkgdir}/opt/spotify/libs/libplc4.so.0d"

  # libnspr4.so
  ln -s /usr/lib/libnspr4.so "${pkgdir}/opt/spotify/libs/libnspr4.so.0d"

  # libnss3.so
  ln -s /usr/lib/libnss3.so "${pkgdir}/opt/spotify/libs/libnss3.so.1d"

  # libnssutil3.so
  ln -s /usr/lib/libnssutil3.so "${pkgdir}/opt/spotify/libs/libnssutil3.so.1d"

  # libsmime3.so
  ln -s /usr/lib/libsmime3.so "${pkgdir}/opt/spotify/libs/libsmime3.so.1d"

  # libssl
  ln -s /usr/lib/libcrypto.so "${pkgdir}/opt/spotify/libs/libcrypto.so.0.9.8"
  ln -s /usr/lib/libssl.so "${pkgdir}/opt/spotify/libs/libssl.so.0.9.8"

  # Copy license
  install -Dm644 "${pkgdir}/usr/share/doc/${pkgname}-client/copyright" "${pkgdir}/usr/share/licenses/${pkgname}/copyright"

  # Copy protocol file if KDE is installed
  if [ -f /usr/bin/startkde ]; then
    echo "Installing with KDE support"
    install -Dm644 "${srcdir}/spotify.protocol" "${pkgdir}/usr/share/kde4/services/spotify.protocol"
  fi
}
