# Maintainer: Gadget3000 <gadget3000@msn.com>
# Contributor: Eothred <yngve.levinsen@gmail.com>

pkgname=spotify
pkgver=0.8.8.323
_anotherpkgver=.gd143501.250-1
pkgrel=2
pkgdesc="A proprietary peer-to-peer music streaming service"
arch=('x86_64' 'i686')
license=('custom:"Copyright (c) 2006-2013 Spotify Ltd"')
install=spotify.install
url="http://www.spotify.com"
changelog='spotify.changelog'
options=('!strip')
conflicts=('spotify-beta')

if [ "${CARCH}" = "x86_64" ]; then
  md5sums=('2b7c240a742a45a873b6208f7160b3e0'
  '37ef199620c3257d1a3e3fb304f4d9b9'
  '074fb11422f8947b2afa769d3be68bea')
  _carch=_amd64
  libarch=x86_64-linux-gnu
elif [ "${CARCH}" = "i686" ]; then
  md5sums=('c2f61cd9e2496c06af471bbc1d071f42'
  'e2cc39229a453fb3da9662dcb87a2ad4'
  '5cf4795648486c846946ba8fc0a879f6')
  _carch=_i386
  libarch=i386-linux-gnu
fi

md5sums+=('53702d38b8bb0e1a7738805449214b54'
'ef25ddc5b6bf8fe1a0d64cbd79e1f7b4')

depends=("alsa-lib>=1.0.14" "glibc>=2.6" "libxss" "qtwebkit" "gconf" "libpng12")
optdepends=('desktop-file-utils: Adds URI support to compatible desktop environments'
'ffmpeg-spotify: Adds playback support for local files')
source=("http://repository.spotify.com/pool/non-free/s/${pkgname}/${pkgname}-client_${pkgver}${_anotherpkgver}${_carch}.deb"
"http://mirrors.us.kernel.org/ubuntu/pool/main/n/nspr/libnspr4_4.8.7-0ubuntu3${_carch}.deb"
"http://mirrors.us.kernel.org/ubuntu/pool/main/o/openssl/libssl0.9.8_0.9.8g-4ubuntu3.20${_carch}.deb"
'spotify'
'spotify.protocol')

build() {
  cd "${srcdir}"

  # libnspr4 - ubuntu oneiric
  install -d "${srcdir}/libnspr4"
  ar x "${srcdir}/libnspr4_4.8.7-0ubuntu3${_carch}.deb" > /dev/null
  tar -xzf data.tar.gz -C "${srcdir}/libnspr4"

  # libssl0.9.8 - ubuntu hardy
  install -d "${srcdir}/libssl"
  ar x "${srcdir}/libssl0.9.8_0.9.8g-4ubuntu3.20${_carch}.deb" > /dev/null
  tar -xzf data.tar.gz -C "${srcdir}/libssl"
}

package() {
  cd "${srcdir}"

  ar x "${pkgname}-client_${pkgver}${_anotherpkgver}${_carch}.deb" > /dev/null
  tar -xzf data.tar.gz -C "${pkgdir}"

  # Bin Script
  install -Dm755 "${srcdir}/spotify" "${pkgdir}/usr/bin/spotify"

  # libnspr4.so
  install -Dm644 "${srcdir}/libnspr4/usr/lib/${libarch}/libnspr4.so" "${pkgdir}/opt/spotify/libs/libnspr4.so"
  ln -s ./libnspr4.so "${pkgdir}/opt/spotify/libs/libnspr4.so.0d"

  # libplc4.so
  install -Dm644 "${srcdir}/libnspr4/usr/lib/${libarch}/libplc4.so" "${pkgdir}/opt/spotify/libs/libplc4.so"
  ln -s ./libplc4.so "${pkgdir}/opt/spotify/libs/libplc4.so.0d"

  # libnss3.so
  ln -s /usr/lib/libnss3.so "${pkgdir}/opt/spotify/libs/libnss3.so.1d"

  # libnssutil3.so
  ln -s /usr/lib/libnssutil3.so "${pkgdir}/opt/spotify/libs/libnssutil3.so.1d"

  # libsmime3.so
  ln -s /usr/lib/libsmime3.so "${pkgdir}/opt/spotify/libs/libsmime3.so.1d"

  # libssl
  install -Dm644 "${srcdir}/libssl/usr/lib/libcrypto.so.0.9.8" "${pkgdir}/opt/spotify/libs/libcrypto.so.0.9.8"
  install -Dm644 "${srcdir}/libssl/usr/lib/libssl.so.0.9.8" "${pkgdir}/opt/spotify/libs/libssl.so.0.9.8"

  # Copy license
  install -Dm644 "${pkgdir}/usr/share/doc/${pkgname}-client/copyright" "${pkgdir}/usr/share/licenses/${pkgname}/copyright"

  # Copy protocol file if KDE is installed
  if [ -f /usr/bin/startkde ]; then
    echo "Installing with KDE support"
    install -Dm644 "${srcdir}/spotify.protocol" "${pkgdir}/usr/share/kde4/services/spotify.protocol"
  fi
}
