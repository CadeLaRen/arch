# Maintainer: John Reese <john@noswap.com>
# Upstream URL: https://github.com/atom/atom
#
# For improvements/fixes to this package, please send a pull request:
# https://github.com/jreese/arch

pkgname=atom-editor
pkgver=0.144.0
pkgrel=1
pkgdesc="Chrome-based text editor from Github"
arch=('x86_64' 'i686')
url="https://github.com/atom/atom"
license=('MIT')
depends=('alsa-lib' 'gconf' 'gtk2' 'libatomic_ops' 'libgcrypt' 'libgnome-keyring' 'libnotify' 'libxtst' 'nodejs' 'nss' 'python2')
makedepends=('git')
source=("https://github.com/atom/atom/archive/v${pkgver}.tar.gz"
        'atom-python.patch')
sha256sums=('0b0b8bd39755d0ac2e8d60a9a273a620b57b4499f372022249c1c2fbc0db0032'
            '222a63a9773c4c406d6fbe7fc06f05535696168a32d39e9be65118a67b935ce6')

_gitref='dc6e825edd8363fa4d789cde1e813ac16d2b9411'
_gitbranch='master'


prepare() {
  cd "atom-$pkgver"

  patch -Np0 -i "$srcdir/atom-python.patch"

  sed -e "s/<%= description %>/$pkgdesc/" \
    -e "s|<%= installDir %>/share/atom/atom|/usr/bin/atom|"\
    -e "s|<%= iconName %>|atom|"\
    resources/linux/atom.desktop.in > resources/linux/Atom.desktop
}

package() {
  cd "$srcdir/atom-$pkgver"

  export PYTHON=python2
  export INSTALL_PREFIX="$pkgdir/usr"
  export JANKY_SHA1=$_gitref
  export JANKY_BRANCH=$_gitbranch

  script/build --install-dir=$pkgdir/usr
  script/grunt install

  install -Dm644 resources/linux/Atom.desktop "$pkgdir/usr/share/applications/atom.desktop"
  install -Dm644 resources/atom.png "$pkgdir/usr/share/pixmaps/atom.png"
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

# vim:set ts=2 sw=2 et:
