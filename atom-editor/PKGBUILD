# Maintainer: John Reese <john@noswap.com>
# Upstream URL: https://github.com/atom/atom
#
# For improvements/fixes to this package, please send a pull request:
# https://github.com/jreese/arch

pkgname=atom-editor
pkgver=0.107.0
pkgrel=1
pkgdesc="Chrome-based text editor from Github"
arch=('x86_64' 'i686')
url="https://github.com/atom/atom"
license=('MIT')
conflicts=('atom-editor-git')
depends=('alsa-lib' 'gconf' 'gtk2' 'libatomic_ops' 'libgcrypt' 'libgnome-keyring' 'libnotify' 'libxtst' 'nodejs' 'nss' 'python2')
source=("https://github.com/atom/atom/archive/v${pkgver}.tar.gz" 'atom-python.patch')
sha256sums=('d4afa7b0fe30e7f4a0270bf68c4cb61ebca7ed0891aa462b756edcb009a01535' 'ecaac2664b33676df2d4f45d24f1754bf801fc1f723ad51d330c69f8c249dcd8')
makedepends=('git')

_gitref='a92eed5107d455e8a893b72e999b6dc235dda699'
_gitbranch='master'

build() {
  cd "$srcdir/atom-$pkgver"

  export PYTHON=python2
  export JANKY_SHA1=$_gitref
  export JANKY_BRANCH=$_gitbranch

  script/build
}

package() {
  cd "$srcdir/atom-$pkgver"

  export PYTHON=python2
  export INSTALL_PREFIX="$pkgdir/usr"
  export JANKY_SHA1=$_gitref
  export JANKY_BRANCH=$_gitbranch

  script/grunt install

  patch "$pkgdir/usr/bin/atom" < "$srcdir/atom-python.patch"

  sed -e "s/<%= description %>/$pkgdesc/" \
    -e "s|<%= installDir %>/share/atom/atom|/usr/bin/atom|"\
    -e "s|<%= iconName %>|atom|"\
    resources/linux/Atom.desktop.in > resources/linux/Atom.desktop
  install -Dm644 resources/linux/Atom.desktop "$pkgdir/usr/share/applications/Atom.desktop"

  install -Dm644 resources/atom.png "$pkgdir/usr/share/pixmaps/atom.png"
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

# vim:set ts=2 sw=2 et:
