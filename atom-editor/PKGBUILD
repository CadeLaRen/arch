# Maintainer: John Reese <john@noswap.com>
# Upstream URL: https://github.com/atom/atom
#
# For improvements/fixes to this package, please send a pull request:
# https://github.com/jreese/arch

pkgname=atom-editor
pkgver=0.94.0
pkgrel=7
pkgdesc="Chrome-based text editor from Github"
arch=('i686' 'x86_64')
url="https://github.com/atom/atom"
license=('MIT')
depends=('libgnome-keyring' 'libgcrypt15' 'libudev.so.0' 'python2' 'nodejs')
source=("https://github.com/atom/atom/archive/v${pkgver}.tar.gz")
sha256sums=('4c4f5cf11faf5598cdd78c94c903cc6775f2de444c94714e2a990635aa4c89bf')
makedepends=('git')

build() {
  cd "$srcdir/atom-$pkgver"
  export PYTHON=python2
  script/build
}

package() {
  cd "$srcdir/atom-$pkgver"
  export PYTHON=python2
  export INSTALL_PREFIX="$pkgdir/usr"
  script/grunt install

  install -d "$pkgdir/usr/share/applications"
  sed -e "s/<%=.*%>/$pkgdesc/" resources/linux/Atom.desktop.in \
    > "$pkgdir/usr/share/applications/Atom.desktop"

  install -d "$pkgdir/usr/share/pixmaps"
  install resources/atom.png "$pkgdir/usr/share/pixmaps/atom.png"
}

# vim:set ts=2 sw=2 et:
