# Maintainer: John Reese <john@noswap.com>
# Upstream URL: https://github.com/atom/atom
#
# For improvements/fixes to this package, please send a pull request:
# https://github.com/jreese/arch

pkgname=atom-editor
pkgver=0.94.0
pkgrel=11
pkgdesc="Chrome-based text editor from Github"
arch=('x86_64')
url="https://github.com/atom/atom"
license=('MIT')
conflicts=('atom-editor-git')
depends=('alsa-lib' 'gconf' 'gtk2' 'libgcrypt15' 'libgnome-keyring' 'libnotify' 'libudev.so.0' 'libxtst' 'nodejs' 'nss' 'python2')
source=("https://github.com/atom/atom/archive/v${pkgver}.tar.gz")
sha256sums=('4c4f5cf11faf5598cdd78c94c903cc6775f2de444c94714e2a990635aa4c89bf')
makedepends=('git')

_gitref='806ff4e141f0eb3cf7e9e829f562915fc7f732ed'
_gitbranch='master'

build() {
  cd "$srcdir/atom-$pkgver"

  export PYTHON=python2
  export JANKY_SHA1=$_gitref
  export JANKY_BRANCH=$_gitbranch

  script/build
}

package() {
  cd "$srcdir/atom-$pkgver"

  export PYTHON=python2
  export INSTALL_PREFIX="$pkgdir/usr"
  export JANKY_SHA1=$_gitref
  export JANKY_BRANCH=$_gitbranch

  script/grunt install

  sed -e "s/<%=.*%>/$pkgdesc/" resources/linux/Atom.desktop.in \
    > resources/linux/Atom.desktop
  install -Dm644 resources/linux/Atom.desktop "$pkgdir/usr/share/applications/Atom.desktop"

  install -Dm644 resources/atom.png "$pkgdir/usr/share/pixmaps/atom.png"
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

# vim:set ts=2 sw=2 et:
