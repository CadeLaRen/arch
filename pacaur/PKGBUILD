# Maintainer: Remy Marquis <remy.marquis@gmail.com>

pkgname=pacaur
pkgver=4.0.1
pkgrel=1
pkgdesc="A fast workflow AUR helper using cower as backend"
arch=('any')
url="https://github.com/Spyhawk/pacaur"
license=('GPL')
depends=('cower' 'expac' 'sudo')
optdepends=('pacman-color: colorized output')
makedepends=('perl')
backup=('etc/xdg/pacaur/config')
source=("https://github.com/Spyhawk/$pkgname/archive/$pkgver.tar.gz")
md5sums=('298aa21e27f43d9732dfba464dedce01')

build() {
  pod2man --utf8 --section=8 --center="Pacaur Manual" --name="PACAUR" --release="$pkgname $pkgver"\
  	$pkgname-$pkgver/README.pod > $pkgname-$pkgver/pacaur.8
}

package() {
  mkdir -p $pkgdir/etc/xdg/pacaur
  install -D -m644 $pkgname-$pkgver/config $pkgdir/etc/xdg/pacaur/config
  mkdir -p $pkgdir/usr/bin
  install -D -m755 $pkgname-$pkgver/$pkgname $pkgdir/usr/bin/$pkgname
  mkdir -p $pkgdir/usr/share/bash-completion/completions
  install -D -m755 $pkgname-$pkgver/bash.completion\
        $pkgdir/usr/share/bash-completion/completions/$pkgname
  mkdir -p $pkgdir/usr/share/man/man8
  install -m 644 $pkgname-$pkgver/pacaur.8 $pkgdir/usr/share/man/man8/pacaur.8 
  mkdir -p $pkgdir/usr/share/locale/{de,fr,ja}/LC_MESSAGES/
  msgfmt $pkgname-$pkgver/po/de.po -o $pkgdir/usr/share/locale/de/LC_MESSAGES/pacaur.mo
  msgfmt $pkgname-$pkgver/po/fr.po -o $pkgdir/usr/share/locale/fr/LC_MESSAGES/pacaur.mo
  msgfmt $pkgname-$pkgver/po/ja.po -o $pkgdir/usr/share/locale/ja/LC_MESSAGES/pacaur.mo
}

