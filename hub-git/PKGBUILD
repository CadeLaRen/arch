# Maintainer: Daniel Wallace: <danielwallace at gtmanfred dot com>
# Contributor: Alfredo Palhares <masterkorp@masterkorp.net>

pkgname=hub-git
_pkgname=hub
pkgver=1.10.5.2.g70a62f8
epoch=1
pkgver(){
    cd $_pkgname
    git describe --tags | sed 's/-/./g;s/^v//'
}
pkgrel=1
pkgdesc="hub introduces git to GitHub"
arch=('any')
url="http://defunkt.io/hub"
depends=('git' 'ruby')
makedepends=('ruby')
license=('MIT')
source=($_pkgname::git://github.com/defunkt/hub.git)
md5sums=('SKIP')
conflicts=('hub' 'hub-rubygem-git')
provides=('hub')
replaces=('hub-rubygem-git')

build() {
    cd "$srcdir/$_pkgname"
    rake standalone
    gzip --best -c man/$_pkgname.1> $_pkgname.1.gz
}

package() {
    cd "$srcdir/$_pkgname"
  install -Dm755 "$_pkgname" "$pkgdir/usr/bin/$_pkgname"

  install -d "$pkgdir/usr/share/$_pkgname"
  cp -dpr --no-preserve=ownership git-hooks "$pkgdir/usr/share/$_pkgname/git-hooks"

  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
  install -Dm644 "etc/hub.bash_completion.sh" "$pkgdir/usr/share/bash-completion/completions/hub"
  install -Dm644 "etc/hub.zsh_completion" "$pkgdir/usr/share/zsh/site-functions/_hub"

  install -Dm644 "man/$_pkgname.1" "$pkgdir/usr/share/man/man1/$_pkgname.1"
}
