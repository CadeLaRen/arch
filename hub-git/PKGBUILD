# Maintainer: Daniel Wallace: <danielwallace at gtmanfred dot com>
# Contributor: Alfredo Palhares <masterkorp@masterkorp.net>

pkgname=hub-git
_pkgname=hub
pkgver=20130105
pkgrel=1
pkgdesc="hub introduces git to GitHub"
arch=('any')
url="http://defunkt.io/hub"
depends=('git' 'ruby')
makedepends=('ruby')
conflicts=('hub' 'hub-rubygem-git')
provides=('hub')
license=('MIT')

_gitroot=git://github.com/defunkt/hub.git
_gitname=hub
build() {
    cd "$srcdir"
    msg "Connecting to GIT server...."

    if [[ -d "$_gitname" ]]; then
        cd "$_gitname" && git pull origin
        msg "The local files are updated."
    else
        git clone "$_gitroot" "$_gitname"
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting build..."

    rm -rf "$srcdir/$_gitname-build"
    git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
    cd "$srcdir/$_gitname-build"

  rake standalone
  gzip --best -c man/$_gitname.1> $_gitname.1.gz
}

package() {
  cd "$srcdir/$_gitname-build"

  install -Dm755 "$_pkgname" "$pkgdir/usr/bin/$_pkgname"

  install -d "$pkgdir/usr/share/$_pkgname"
  cp -dpr --no-preserve=ownership git-hooks "$pkgdir/usr/share/$_pkgname/git-hooks"

  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
  install -Dm644 "etc/hub.bash_completion.sh" "$pkgdir/usr/share/bash-completion/completions/hub"
  install -Dm644 "etc/hub.zsh_completion" "$pkgdir/usr/share/zsh/site-functions/_hub"

  install -Dm644 "man/$_pkgname.1" "$pkgdir/usr/share/man/man1/$_pkgname.1"
}
