# Maintainer: John Reese <john@noswap.com>
# Upstream URL: https://github.com/atom/atom
#
# For improvements/fixes to this package, please send a pull request:
# https://github.com/jreese/arch


pkgname=atom-editor-git
pkgver=20140507
pkgrel=1
pkgdesc="Chrome-based text editor from Github"
arch=('x86_64')
url="https://github.com/atom/atom"
license=('MIT')
conflicts=('atom-editor')
depends=('alsa-lib' 'gconf' 'gtk2' 'libgcrypt15' 'libgnome-keyring' 'libnotify' 'libudev.so.0' 'libxtst' 'nodejs' 'nss' 'python2')
makedepends=('git')

_gitroot="git://github.com/atom/atom"
_gitname="atom"

source=("$_gitroot")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  git log -1 --format="%cd" --date=short | sed 's|-||g'
}

build() {
  cd "$srcdir/$_gitname"
  export PYTHON=python2
  script/build
}

package() {
  cd "$srcdir/$_gitname"
  export PYTHON=python2
  export INSTALL_PREFIX="$pkgdir/usr"
  script/grunt install

  install -d "$pkgdir/usr/share/applications"
  sed -e "s/<%=.*%>/$pkgdesc/" resources/linux/Atom.desktop.in \
    > "$pkgdir/usr/share/applications/Atom.desktop"

  install -D resources/atom.png "$pkgdir/usr/share/pixmaps/atom.png"
  install -D LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

# vim:set ts=2 sw=2 et:
