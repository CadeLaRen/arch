# Maintainer: Linus Karlsson <arch at zozs dot se>
# Contributor: Peter Hultqvist <endnode dot se>
pkgname=ykpers-git
pkgver=20140223
pkgrel=1
pkgdesc="A small library for personalization of Yubico's USB key"
arch=('i686' 'x86_64' 'armv6h')
url="http://yubico.github.io/yubikey-personalization"
license=('custom:BSD')
depends=('libyubikey-git')
makedepends=('git')
conflicts=('ykpers')
options=('!libtool')
_gitname='yubikey-personalization'
_gitroot="git://github.com/Yubico/$_gitname.git"
source=("$_gitroot")
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  git log -1 --format="%cd" --date=short | sed 's|-||g'
}

build() {
	cd "$srcdir/$_gitname"
	autoreconf --install
	./configure --prefix=/usr --with-udevrulesdir=/usr/lib/udev/rules.d
	make
}

package() {
	cd "$srcdir/$_gitname"
	make DESTDIR="$pkgdir/" install

	install -D -m0644 $srcdir/$_gitname/COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}

