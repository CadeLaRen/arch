# Maintainer: Christian Hesse <mail@eworm.de

pkgname=libyubikey
pkgver=1.10
pkgrel=1
pkgdesc="Yubico C low-level library"
arch=('i686' 'x86_64')
url="http://code.google.com/p/yubico-c/"
license=('custom:BSD')
depends=('libusb')
install=libyubikey.install
source=("https://yubico-c.googlecode.com/files/${pkgname}-${pkgver}.tar.gz"
	'70-yubikey.rules')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}/

	./configure --prefix=/usr
	make
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}/

	make DESTDIR="${pkgdir}/" install

	install -D -m0644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
	install -D -m0644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
	install -D -m0644 "${srcdir}/70-yubikey.rules" "${pkgdir}/usr/lib/udev/rules.d/70-yubikey.rules"
}

sha256sums=('8cabfe4a547550bcf6eea1fa830fd78bfdef35d1368de5455a29cfdfa30bc00a'
            '5ae001cd6356115123a454db8dcdb1392aa5167460b52ee6e68f3509b4c3aa86')
