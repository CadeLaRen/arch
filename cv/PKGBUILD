# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=cv
pkgdesc="Shows running coreutils basic commands and displays stats"
pkgver=0.3
pkgrel=1
arch=('i686' 'x86_64')
makedepends=('make')
url="https://github.com/Xfennec/cv"
license=('GPL3')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/Xfennec/$pkgname/tar.gz/v$pkgver)
sha256sums=('3bca828c80cebb42a3e4fad26c15e47c70e65efb103bd0b9149b8ff704ae1117')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  msg 'Fixing Makefile...'
  sed -i "s@/usr/local@/usr@" Makefile
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg 'Building...'
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg 'Installing license...'
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  msg 'Installing documentation...'
  for _doc in capture.png README.md; do
    install -Dm 644 "$_doc" "$pkgdir/usr/share/doc/$pkgname/$_doc"
  done

  msg 'Installing...'
  install -dm 755 "$pkgdir/usr/bin"
  make DESTDIR="$pkgdir" install
}
