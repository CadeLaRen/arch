# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributer: HuntXu <huntxu@live.cn>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=pngcrush
pkgver=1.7.58
pkgrel=1
pkgdesc="A tool for optimizing the compression of PNG files"
arch=('i686' 'x86_64')
url="http://pmt.sourceforge.net/"
license=('custom')
depends=('glibc')
source=("http://downloads.sourceforge.net/pmt/$pkgname-$pkgver.tar.xz"
        LICENSE)
md5sums=('50eaf317c6d6d3f3a1cfa7ce659cff48'
         'e6446a786d939c98fcada57f9ae6de1f')

prepare() {
 cd "$srcdir"/$pkgname-$pkgver
  # fix for better compression
  sed -i 's|TOO_FAR 4096|TOO_FAR 32767|' deflate.c
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  CFLAGS+=" -DPNGCRUSH_COUNT_COLORS" make
}

package() {
  install -Dm755 "$srcdir"/$pkgname-$pkgver/$pkgname \
    "$pkgdir"/usr/bin/$pkgname
  install -Dm644 $srcdir/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
